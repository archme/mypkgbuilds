# Puppet: Installer: Arch
# Maintainer: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>

pkgname=puppet1.8
_pkgname=puppet
pkgver=0.25.4
rubyver=1.8
pkgrel=1
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://reductivelabs.com/projects/${_pkgname}/"
license=("GPL")
depends=("ruby1.8" "facter1.8" "ruby1.8-augeas")
optdepends=("libshadow-ruby: Enables password management for the 'useradd' provider")
options=(emptydirs)
install="${_pkgname}.install"
source=(http://reductivelabs.com/downloads/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	# Variables
	export DESTDIR=${pkgdir}

	# Build
	ruby-1.8 ./install.rb || return 1

	# Directories
	install -d ${pkgdir}/etc/{${_pkgname},rc.d}
	install -d ${pkgdir}/etc/${_pkgname}/{ssl,manifests}
	install -d ${pkgdir}/etc/${_pkgname}/ssl/{ca,certs,private}
	install -d ${pkgdir}/etc/${_pkgname}/ssl/{private_keys,public_keys}
	install -d ${pkgdir}/var/run/${_pkgname}
	install -d ${pkgdir}/var/lib/${_pkgname}/{templates,lib,facts}
	install -d ${pkgdir}/var/lib/${_pkgname}/{rrd,reports,clientbucket}
	install -d ${pkgdir}/var/lib/${_pkgname}/{yaml,client_yaml,bucket}
	install -d ${pkgdir}/var/lib/${_pkgname}/{plugins,state}
	#install -d ${pkgdir}/var/lib/${_pkgname}/state/{graphs,localconfig}
	install -d ${pkgdir}/var/lib/${_pkgname}/state/graphs
	install -d ${pkgdir}/var/log/${_pkgname}

	cd ${startdir}

	# Configuration
	install -D extra/${_pkgname}.conf ${pkgdir}/etc/${_pkgname}/${_pkgname}.conf
	install -D extra/fileserver.conf ${pkgdir}/etc/${_pkgname}/fileserver.conf
	install -D extra/${_pkgname}master.conf ${pkgdir}/etc/${_pkgname}/${_pkgname}master.conf

	# Daemons
	install -D extra/${_pkgname} ${pkgdir}/etc/rc.d/${_pkgname}
	install -D extra/${_pkgname}master ${pkgdir}/etc/rc.d/${_pkgname}master

	# Pacman Provider
	install -m 644 extra/pacman.rb ${pkgdir}/opt/ruby1.8/lib/ruby/site_ruby/${rubyver}/${_pkgname}/provider/package/pacman.rb
	install -m 644 extra/arch.rb ${pkgdir}/opt/ruby1.8/lib/ruby/site_ruby/${rubyver}/${_pkgname}/provider/service/arch.rb
}

sha1sums=('290dcc20eb670b5b8e26d50301430e1333a037ab')
